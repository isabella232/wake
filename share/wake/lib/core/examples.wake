# Copyright 2021 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
package wake

def expectOne s = match _
    x, Nil = Pass x
    _      = failWithError s

# Ported from the jq tutorial
export def example1 (j: JValue): Result JValue Error = Pass j
export def example2 (j: JValue): Result JValue Error =
    j | jAt 0 | expectOne "j was empty"

# Simplify github json first commit
export def example3 (log: JValue): Result JValue Error =
    require Pass commit =
        single log
        |> jAt 0
        | expectOne "expected at least one commit"

    require Pass message =
        single commit
        |> jField "commit"
        |> jField "message"
        | expectOne ".commit.message missing"

    require Pass name =
        single commit
        |> jField "commit"
        |> jField "name"
        | expectOne ".commit.name missing"

    def output = JObject (
        "message" :-> message,
        "name" :-> name,
        Nil
    )

    Pass output

# Simplify full github json log
export def example4 (log: JValue): Result JValue Error =
    def simpleCommit commit =
        require Pass message =
            single commit
            |> jField "commit"
            |> jField "message"
            | expectOne ".commit.message missing"

        require Pass name =
            single commit
            |> jField "commit"
            |> jField "name"
            | expectOne ".commit.name missing"

        def output = JObject (
            "message" :-> message,
            "name" :-> name,
            Nil
        )

        Pass output

    single log
    |> jFlatten
    | findFailFn simpleCommit
    | rmap JArray

# Simplify full github json log but keep parent urls
export def example5 (log: JValue): Result JValue Error =
    def simpleCommit commit =
        require Pass message =
            single commit
            |> jField "commit"
            |> jField "message"
            | expectOne ".commit.message missing"

        require Pass name =
            single commit
            |> jField "commit"
            |> jField "name"
            | expectOne ".commit.name missing"

        def parents =
            single commit
            |> jField "parents"
            |> jFlatten
            |> jField "html_url"
            | JArray

        def output = JObject (
            "message" :-> message,
            "name" :-> name,
            "parents" :-> parents,
            Nil
        )

        Pass output

    single log
    |> jFlatten
    | findFailFn simpleCommit
    | rmap JArray

# Convert a git log to a simplified wake tuple
export tuple Commit =
    Message: String
    Name: String
    Parents: List String

# Simplify github json log
export def example6 (log: JValue): Result (List Commit) Error =
    def simpleCommit commit =
        require Pass message =
            single commit
            |> jField "commit"
            |> jField "message"
            |> jString
            | expectOne ".commit.message was missing or not a string"

        require Pass name =
            single commit
            |> jField "commit"
            |> jField "name"
            |> jString
            | expectOne ".commit.name missing or not a string"

        def parents =
            single commit
            |> jField "parents"
            |> jFlatten
            |> jField "html_url"
            |> jString

        def output = Commit message name parents

        Pass output

    # This is a bit complicated IMO but not too bad
    single log
    |> jFlatten
    | findFailFn simpleCommit

export def example7 (json: JValue): List JValue =
    single json
    |> jField "instances"
    |  filter (_ | jField "instance_name" | jExists "testIndicator".JString)

# It turns out, edit functions compose with `$` in the order you want
# which is somehow surprising and obvious at the same time. Who knew?
export def exampleEdit1: Pair Integer b => Pair Integer b =
    editPairFirst $ (_+4)

export def exampleEdit2: Triple (Pair a Integer) b c => Triple (Pair a Integer) b c =
    editTripleFirst $ editPairSecond $ (_+4)

export def exampleEdit3: JValue => JValue =
    editJField "hello"
    $ editJField "bar"
    $ editJString
    $ ("zzz{_}zzz")
