# Copyright 2021 SiFive, Inc.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You should have received a copy of LICENSE.Apache2 along with
# this software. If not, you may obtain a copy at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package build_wake
from wake import _
from gcc_wake import _

def buildWakeBox (Pair (cpplib: String) (clib: String)) : Result (List Path) Error =
    require Pass (commonLib: SysLib) =
        common cpplib

    require Pass (goptLib: SysLib) =
        gopt clib

    require Pass (fuseLib: List Path) =
        buildFuseLibObjs cpplib

    def libs : SysLib =
        (commonLib, goptLib, Nil)
        | flattenSysLibs

    def headers : List Path =
        sources "fuse" `.*\.h` ++
        goptLib.getSysLibHeaders ++
        commonLib.getSysLibHeaders

    def compile (in: Path) : Result (List Path) Error =
        compileC cpplib ("-Ifuse", goptLib.getSysLibCFlags ++ commonLib.getSysLibCFlags) headers in

    def cppFiles : List Path =
        sources "wakebox" `.*\.cpp`

    require Pass objs =
        map compile cppFiles
        | findFail

    def objFiles : List Path =
        fuseLib ++
        goptLib.getSysLibObjects ++
        objs.flatten

    require Pass paths =
        linkO cpplib libs.getSysLibLFlags objFiles "bin/wakebox"

    require Pass _ =
        match paths
            bin, Nil = installAs "bin/wakebox" bin | getPathResult
            _        = Fail "missing wakebox executable".makeError

    Pass paths
